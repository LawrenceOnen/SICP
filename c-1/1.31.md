a.  The sum procedure is only the simplest of a vast number of similar abstractions that can be captured as higher-order procedures.
Write an analogous procedure called product that returns the product of the values of a function at points over a given range. Show how to define factorial in terms of product. Also use product to compute approximations to π using the formula

```
π/4 = (2*4*4*6*6*8...)/(3*3*5*5*7*7...)
```

b.  If your product procedure generates a recursive process, write one that generates an iterative process. If it generates an iterative process, write one that generates a recursive process.

factorial: n! = n * (n - 1)!

```
(define (product term a next b)
  (if (> a b)
      1
      (* (term a)
         (product term (next a) next b))))

(define (product-iter term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (* result (term a)))))
  (iter a 1))

(define (factorial n)
    (define (term k)
        (if (= k 0)
            1
            k))
    (define (next k)
        (+ k 1))
    (product term 0 next n))

(define (approximate-pi n)
    (define (inc x)
        (+ x 1))
    (define (odd? n)
        (= (remainder n 2) 1))
    (define (app n)
        (define (term x)
            (cond ((odd? x) (/ (+ x 1) (+ x 2)))
                  (else (/ (+ x 2) (+ x 1)))))
        (product-iter term 1 inc n))
    (* 4 (app n)))
```
