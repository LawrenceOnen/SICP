Most Lisp implementations include a primitive called runtime that returns an integer that specifies the amount of time the system has been running (measured, for example, in microseconds). The following timed-prime-test procedure, when called with an integer n, prints n and checks to see if n is prime. If n is prime, the procedure prints three asterisks followed by the amount of time used in performing the test.

```scheme
(define (square n)
  (* n n))
(define (smallest-divisor n)
  (find-divisor n 2))
(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
        (else (find-divisor n (+ test-divisor 1)))))
(define (divides? a b)
  (= (remainder b a) 0))

(define (prime? n)
  (= n (smallest-divisor n)))

  (define (timed-prime-test n)
    (newline)
    (display n)
    (start-prime-test n (runtime)))
  (define (start-prime-test n start-time)
    (if (prime? n)
        (report-prime (- (runtime) start-time))))
  (define (report-prime elapsed-time)
    (display " *** ")
    (display (* elapsed-time 1000)))

(define (even? n)
  (= (remainder n 2) 0))
```

Using this procedure, write a procedure search-for-primes that checks the primality of consecutive odd integers in a specified range. Use your procedure to find the three smallest primes larger than 1000; larger than 10,000; larger than 100,000; larger than 1,000,000. Note the time needed to test each prime. Since the testing algorithm has order of growth of θ(√n), you should expect that testing for primes around 10,000 should take about √10 times as long as testing for primes around 1000. Do your timing data bear this out? How well do the data for 100,000 and 1,000,000 support the √n prediction? Is your result compatible with the notion that programs on your machine run in time proportional to the number of steps required for the computation?

```scheme
(define (search-for-primes start count)
  (define (search start count start-time)
    (cond ((= count 0) (newline) (display "done"))
          ((even? start) (search (+ start 1) count start-time))
          ((prime? start) (newline)
                          (display start)
                          (report-prime (- (runtime) start-time))
                          (search (+ start 2) (- count 1) (runtime)))
          (else (search (+ start 2) count start-time))))
  (search start count (runtime)))

(search-for-primes 100000000000 3)
;100000000003 *** 310
;100000000019 *** 320
;100000000057 *** 360

(search-for-primes 10000000000 3)
;10000000019 *** 150
;10000000033 *** 110
;10000000061 *** 110

(search-for-primes 1000000000 3)
;1000000007 *** 40
;1000000009 *** 30
;1000000021 *** 40

(search-for-primes 100000000 3)
;100000007 *** 20
;100000037 *** 10
;100000039 *** 10
```

The answer is Yes.
