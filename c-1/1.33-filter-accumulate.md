You can obtain an even more general version of accumulate (exercise 1.32) by introducing the notion of a filter on the terms to be combined. That is, combine only those terms derived from values in the range that satisfy a specified condition. The resulting filtered-accumulate abstraction takes the same arguments as accumulate, together with an additional predicate of one argument that specifies the filter. Write filtered-accumulate as a procedure. Show how to express the following using filtered-accumulate:

a. the sum of the squares of the prime numbers in the interval a to b (assuming that you have a prime? predicate already written)

b. the product of all the positive integers less than n that are relatively prime to n (i.e., all positive integers i < n such that GCD(i,n) = 1).

```scheme
(define (filter-accumulate combiner filter null-value term a next b)
    (if (> a b)
        null-value
        (if (filter a)
            (combiner (term a) (filter-accumulate combiner filter null-value term (next a) next b))
            (filter-accumulate combiner filter null-value term (next a) next b))))

(define (square n)
  (* n n))
(define (smallest-divisor n)
  (find-divisor n 2))

(define (find-divisor n test-divisor)
  (define (next n)
      (if (= n 2)
          3
          (+ n 2)))
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
        (else (find-divisor n (next test-divisor)))))

(define (divides? a b)
  (= (remainder b a) 0))

(define (prime? n)
  (= n (smallest-divisor n)))
```

**Answer to a:**

```scheme
(define (sum-of-square-of-primes-between a b)
    (filter-accumulate + prime? 0 square a (lambda (x) (+ x 1)) b)
)

(sum-of-square-of-primes-between 2 8) ;-> 87
```

**Answer to b:**

```scheme
(define (gcd a b)
   (if (= b 0)
       a
       (gcd b (remainder a b))))

(define (product-of-positive-int-less-than n)
    (define (identity x)
        x)
    (filter-accumulate * (lambda (x) (= (gcd x n) 1)) 1 identity 0 (lambda (x) (+ x 1)) n))

(product-of-positive-int-less-than 10)
```

