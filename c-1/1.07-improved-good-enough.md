The good-enough? test used in computing square roots will not be very effective for finding the square roots of very small numbers. Also, in real computers, arithmetic operations are almost always performed with limited precision. This makes our test inadequate for very large numbers. Explain these statements, with examples showing how the test fails for small and large numbers. An alternative strategy for implementing good-enough? is to watch how guess changes from one iteration to the next and to stop when the change is a very small fraction of the guess. Design a square-root procedure that uses this kind of end test. Does this work better for small and large numbers?

Original version:

```scheme
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))

(define (improve guess x)
  (average guess (/ x guess)))

(define (average x y)
  (/ (+ x y) 2))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (square x) (* x x))

(define (sqrt x)
  (sqrt-iter 1.0 x))
```

When the value x is around 0.001, the `improve` function is gradually reduced by half to value roughly near 0.5, 0.25, 0.125, etc.  The `good-enough?` method is calculating a fixed threshold 0.001 between square of the guess and x.  When the guess is somewhere near 0.03 whose square value is already 0.0009, the difference between it and value x is smaller the threshold already.  Hence, no matter what value x smaller than 0.0009, square root value is always around 0.03.

Small sample value: 0.000001.


As the values grow larger, the precision of the guess comes into play.  Eventually the computer becomes unable to represent the guess to a precision of 0.001.  In such cases, the program can endlessly alternate between two guesses that are more than 0.001 away from the true square root.

Large sample value: 1e13


Improved version

```scheme
(define (sqrt-iter guess x lastguess)
  (if (good-enough? guess lastguess)
      guess
      (sqrt-iter (improve guess x)
                 x guess)))

(define (improve guess x)
  (average guess (/ x guess)))

(define (average x y)
  (/ (+ x y) 2))

(define (good-enough? guess lastguess)
    (< (/ (abs (-  guess lastguess)) guess) 0.001)
)

(define (sqrt x)
  (sqrt-iter 1.0 x 0))
```
