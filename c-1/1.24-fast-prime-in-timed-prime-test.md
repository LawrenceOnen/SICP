Modify the timed-prime-test procedure of exercise 1.22 to use fast-prime? (the Fermat method), and test each of the 12 primes you found in that exercise. Since the Fermat test has Î¸(log n) growth, how would you expect the time to test primes near 1,000,000 to compare with the time needed to test primes near 1000? Do your data bear this out? Can you explain any discrepancy you find?

```scheme
(define (expmod base exp m)
  (cond ((= exp 0) 1)
        ((even? exp)
         (remainder (square (expmod base (/ exp 2) m))
                    m))
        (else
         (remainder (* base (expmod base (- exp 1) m))
                    m))))        

(define (fermat-test n)
  (define (try-it a)
    (= (expmod a n n) a))
  (try-it (+ 1 (random-integer (- n 1)))))

(define (fast-prime? n times)
  (cond ((= times 0) true)
        ((fermat-test n) (fast-prime? n (- times 1)))
        (else false)))

  (define (report-prime elapsed-time)
    (display " *** ")
    (display (* elapsed-time 1000)))

(define (search-for-primes start count)
  (define (search start count start-time)
    (cond ((= count 0) (newline) (display "done"))
          ((even? start) (search (+ start 1) count start-time))
          ((fast-prime? start 10000) (newline)
                          (display start)
                          (report-prime (- (runtime) start-time))
                          (search (+ start 2) (- count 1) (runtime)))
          (else (search (+ start 2) count start-time))))
  (search start count (runtime)))


(search-for-primes 10000000000000 3)
;10000000000037 *** 660
;10000000000051 *** 670
;10000000000099 *** 659

(search-for-primes 1000000000000 3)
;1000000000039 *** 630
;1000000000061 *** 640
;1000000000063 *** 650

(search-for-primes 100000000000 3)
;100000000003 *** 580
;100000000019 *** 620
;100000000057 *** 619

(search-for-primes 10000000000 3)
;10000000019 *** 530
;10000000033 *** 550
;10000000061 *** 559

(search-for-primes 1000000000 3)
;1000000007 *** 450
;1000000009 *** 430
;1000000021 *** 449

(search-for-primes 100000000 3)
;100000007 *** 370
;100000037 *** 380
;100000039 *** 390

(search-for-primes 10000000 3)
;10000019 *** 320
;10000079 *** 330
;10000103 *** 340

(search-for-primes 1000000 3)
;1000003 *** 260
;1000033 *** 270
;1000037 *** 269

(search-for-primes 10000 3)
;10007 *** 210
;10009 *** 190
;10037 *** 200
```


Testing a number with twice as many digits (1e12 vs. 1e6) is roughly twice as slow. This supports the theory of logarithmic growth.
