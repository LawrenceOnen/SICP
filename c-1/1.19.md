There is a clever algorithm for computing the Fibonacci numbers in a logarithmic number of steps. Recall the transformation of the state variables a and b in the fib-iter process of section 1.2.2: a ⟵ a + b and b ⟵ a. Call this transformation T, and observe that applying T over and over again n times, starting with 1 and 0, produces the pair Fib(n + 1) and Fib(n). In other words, the Fibonacci numbers are produced by applying Tn, the nth power of the transformation T, starting with the pair (1,0). Now consider T to be the special case of p = 0 and q = 1 in a family of transformations Tpq, where Tpq transforms the pair (a,b) according to a ⟵ bq + aq + ap and b ⟵ bp + aq. Show that if we apply such a transformation Tpq twice, the effect is the same as using a single transformation Tp'q' of the same form, and compute p' and q' in terms of p and q. This gives us an explicit way to square these transformations, and thus we can compute Tn using successive squaring, as in the fast-expt procedure. Put this all together to complete the following procedure, which runs in a logarithmic number of steps:

Original version:

```scheme
(define (fib n)
  (fib-iter 1 0 n))

(define (fib-iter a b count)
  (if (= count 0)
      b
      (fib-iter (+ a b) a (- count 1))))
```

Refine version:

```scheme
(define (double x)
    (* x x))

(define (fib n)
  (fib-iter 1 0 0 1 n))
(define (fib-iter a b p q count)
  (cond ((= count 0) b)
        ((even? count)
         (fib-iter a
                   b
                   (+ (double p) (double q))      ; compute p'
                   (+ (double q) (* 2 p q))      ; compute q'
                   (/ count 2)))
        (else (fib-iter (+ (* b q) (* a q) (* a p))
                        (+ (* b p) (* a q))
                        p
                        q
                        (- count 1)))))

;Tpq
;a = bq + aq + ap
;b = bp + aq

;Tpq^2
;a = (bp + aq)q + (bq + aq + ap)q + (bq + aq + ap)p
;    bpq + aq^2 + bq^2 + aq^2 + apq + bpq + aqp + ap^2
;    b(q^2 + 2pq) + a(q^2 + p^2) + a(q^2 + 2qp)

;b = (bp + aq)p + (bq + aq + ap)q
;    bp^2 + aqp + bq^2 + aq^2 + apq
;    b(p^2 + q^2) + a(q^2 + 2pq)

;p1 = p^2 + q^2
;q1 = q^2 + 2pq
```
