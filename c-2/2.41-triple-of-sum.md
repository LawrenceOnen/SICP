Write a procedure to find all ordered triples of distinct positive integers `i`, `j`, and `k` less than or equal to a given integer `n` that sum to a given integer `s`.

```scheme
(define (filter predicate sequence)
  (cond ((null? sequence) '())
        ((predicate (car sequence))
         (cons (car sequence)
               (filter predicate (cdr sequence))))
        (else (filter predicate (cdr sequence)))))

(define (enumerate-interval s e)
    (define (iter result k)
        (if (> k e)
            result
            (iter (append result (list k)) (+ k 1))))
    (iter '() s))

(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))

(define (flatmap proc seq)
  (accumulate append '() (map proc seq)))

(define (triple-of-sum n s)
   (filter (lambda (x) (= s (accumulate + 0 x)))
           (flatmap
            (lambda (i)
              (flatmap (lambda (j)
                     (map (lambda (k)
                            (list i j k))
                          (enumerate-interval 1 (- j 1))))
                   (enumerate-interval 1 (- i 1))))
            (enumerate-interval 1 n))))

(triple-of-sum 11 10) ; ((5 3 2) (5 4 1) (6 3 1) (7 2 1))
```
