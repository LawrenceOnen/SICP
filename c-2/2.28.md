Write a procedure fringe that takes as argument a tree (represented as a list) and returns a list whose elements are all the leaves of the tree arranged in left-to-right order. For example,

```scheme
(define x (list (list 1 2) (list 3 4)))

(fringe x)
(1 2 3 4)

(fringe (list x x))
(1 2 3 4 1 2 3 4)
```

**Answer**

```scheme
(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))

(define (fringe x)
    (if (null? x)
        '()
        (let ((h (car x))
              (t (cdr x)))
            (if (pair? h)
                (append (fringe h) (if (pair? t) (fringe t) t))
                (cons h (if (pair? t) (fringe t) t))))))

; Verify 
(fringe (list x 5 6 (list 7 8) x))

(fringe (list 5 x (list 7 x 8 x)))

(fringe (list x x 5 x))

; Another two solutions for reference

; From http://www.billthelizard.com/2011/02/sicp-228-flattening-nested-lists.html
(define (fringe x)
    (cond ((null? x) x)
          ((not (pair? x)) (list x))
          (else (append (fringe (car x)) (fringe (cdr x))))))

; http://kelvinh.github.io/wiki/sicp/#sec-2-28
(define (fringe items)
  (define (iter items result)
    (cond ((null? items) result)
          ((not (pair? items)) (append result (list items)))
          (else (iter (cdr items) (iter (car items) result)))))
  (iter items '()))
```
