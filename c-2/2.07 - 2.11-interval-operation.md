```scheme
(define (add-interval x y)
  (make-interval (+ (lower-bound x) (lower-bound y))
                 (+ (upper-bound x) (upper-bound y))))

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (div-interval x y)
  (mul-interval x 
                (make-interval (/ 1.0 (upper-bound y))
                               (/ 1.0 (lower-bound y)))))
```

Alyssa's program is incomplete because she has not specified the implementation of the interval abstraction. Here is a definition of the interval constructor:

```scheme
(define (make-interval a b) (cons a b))
```

**Exercise 2.7**

Define selectors `upper-bound` and `lower-bound` to complete the implementation.

```scheme
(define (upper-bound x)
    (cdr x))

(define (lower-bound x)
    (car x))
```

**Exercise 2.8**

Using reasoning analogous to Alyssa's, describe how the difference of two intervals may be computed. Define a corresponding subtraction procedure, called sub-interval.

**Answer**

[a, b] - [c, d] = [a - d, b - c]

```scheme
(define (sub-interval x y)
     (make-interval (- (lower-bound x) (upper-bound y))
                    (- (upper-bound x) (lower-bound y))))

; Verify
(define a (make-interval 0 50))
(define b (make-interval 50 100))
(define c (make-interval 30 70))

(sub-interval b a) ; (0 . 100)
(sub-interval b c) ; (-20 . 70)
(sub-interval a c) ; (-70 . 20)
```

**Exercise 2.9**

The width of an interval is half of the difference between its upper and lower bounds. The width is a measure of the uncertainty of the number specified by the interval. For some arithmetic operations the width of the result of combining two intervals is a function only of the widths of the argument intervals, whereas for others the width of the combination is not a function of the widths of the argument intervals. Show that the width of the sum (or difference) of two intervals is a function only of the widths of the intervals being added (or subtracted). Give examples to show that this is not true for multiplication or division.

```scheme
(define (interval-width x)
    (/ (- (upper-bound x) (lower-bound x)) 2.0))

; Verify

(define a (make-interval 0 20))
(define b (make-interval 40 60))
(define c (make-interval 50 70))

(interval-width (mul-interval a b)) ; 600
(* (interval-width a) (interval-width b)) ; 100

(interval-width (div-interval c b)) ; .4583333333333333
(/ (interval-width c) (interval-width b)) ; 1
```

**Exercise 2.10**

Ben Bitdiddle, an expert systems programmer, looks over Alyssa's shoulder and comments that it is not clear what it means to divide by an interval that spans zero. Modify Alyssa's code to check for this condition and to signal an error if it occurs.

```scheme
(define (span-zero? x)
    (and (>= (upper-bound x) 0)
         (<= (lower-bound x) 0)))

(define (div-interval-w-err x y)
    (if (span-zero? y)
       (error "Error: the denominator spans 0.")
       (mul-interval x 
                (make-interval (/ 1.0 (upper-bound y))
                               (/ 1.0 (lower-bound y))))))

; Verify

(define a (make-interval 2 3))
(define b (make-interval -1 1))
(div-interval-w-err a b)
```

**Exercise 2.11**

In passing, Ben also cryptically comments: “By testing the signs of the endpoints of the intervals, it is possible to break mul-interval into nine cases, only one of which requires more than two multiplications.” Rewrite this procedure using Ben's suggestion.

```scheme
(define (mul-interval x y)
    (let ((lx (lower-bound x))
          (ly (lower-bound y))
          (ux (upper-bound x))
          (uy (upper-bound y)))
        (cond ((and (>= lx 0)
                    (>= ux 0)
                    (>= ly 0)
                    (>= uy 0)) ; [+ +] * [+ +]
                (make-interval (* lx ly) (* ux uy)))
              ((and (>= lx 0)
                    (>= ux 0)
                    (<= ly 0)
                    (>= uy 0)) ; [+ +] * [- +]
                (make-interval (* ux ly) (* ux uy)))
              ((and (>= lx 0)
                    (>= ux 0)
                    (<= ly 0)
                    (<= uy 0)) ; [+ +] * [- -]
                (make-interval (* ux ly) (* lx uy)))
              ((and (<= lx 0)
                    (>= ux 0)
                    (>= ly 0)
                    (>= uy 0)) ; [- +] * [+ +]
                (make-interval (* lx uy) (* ux uy)))
              ((and (<= lx 0)
                    (>= ux 0)
                    (<= ly 0)
                    (>= uy 0)) ; [- +] * [- +]
                (make-interval
                    (min (* lx uy) (* ux ly))
                    (max (* lx ly) (* ux uy))))
              ((and (<= lx 0)
                    (>= ux 0)
                    (<= ly 0)
                    (<= uy 0)) ; [- +] * [- -]
                (make-interval (* ux ly) (* lx ly)))
              ((and (<= lx 0)
                    (<= ux 0)
                    (>= ly 0)
                    (>= uy 0)) ; [- -] * [+ +]
                (make-interval (* lx uy) (* ux ly)))
              ((and (<= lx 0)
                    (<= ux 0)
                    (<= ly 0)
                    (>= uy 0)) ; [- -] * [- +]
                (make-interval (* lx uy) (* lx ly)))
              ((and (<= lx 0)
                    (<= ux 0)
                    (<= ly 0)
                    (<= uy 0)) ; [- -] * [- -]
                (make-interval (* ux uy) (* lx ly)))
              )))

; Verify
(define a (make-interval 1 2))
(define b (make-interval -2 1))
(define c (make-interval -2 -1))

(mul-interval a a) ; (1 4)
(mul-interval a b) ; (-4 2)
(mul-interval a c) ; (-4 -1)
(mul-interval b a) ; (-4 2)
(mul-interval b b) ; (-2 4)
(mul-interval b c) ; (-2 4)
(mul-interval c a) ; (-4 -1)
(mul-interval c b) ; (-2 4)
(mul-interval c c) ; (1 4)
```
