The “eight-queens puzzle” asks how to place eight queens on a chessboard so that no queen is in check from any other (i.e., no two queens are in the same row, column, or diagonal). One possible solution is shown in figure 2.8. One way to solve the puzzle is to work across the board, placing a queen in each column. Once we have placed `k - 1` queens, we must place the kth queen in a position where it does not check any of the queens already on the board. We can formulate this approach recursively: Assume that we have already generated the sequence of all possible ways to place `k - 1` queens in the first `k - 1` columns of the board. For each of these ways, generate an extended set of positions by placing a queen in each row of the kth column. Now filter these, keeping only the positions for which the queen in the kth column is safe with respect to the other queens. This produces the sequence of all ways to place `k` queens in the first `k` columns. By continuing this process, we will produce not only one solution, but all solutions to the puzzle.

We implement this solution as a procedure `queens`, which returns a sequence of all solutions to the problem of placing n queens on an `n × n` chessboard. Queens has an internal procedure `queen-cols` that returns the sequence of all ways to place queens in the first `k` columns of the board.

```scheme
(define (queens board-size)
  (define (queen-cols k)  
    (if (= k 0)
        (list empty-board)
        (filter
         (lambda (positions) (safe? k positions))
         (flatmap
          (lambda (rest-of-queens)
            (map (lambda (new-row)
                   (adjoin-position new-row k rest-of-queens))
                 (enumerate-interval 1 board-size)))
          (queen-cols (- k 1))))))
  (queen-cols board-size))
```

In this procedure `rest-of-queens` is a way to place `k - 1` queens in the first `k - 1` columns, and `new-row` is a proposed row in which to place the queen for the kth column. Complete the program by implementing the representation for sets of board positions, including the procedure `adjoin-position`, which adjoins a new row-column position to a set of positions, and `empty-board`, which represents an empty set of positions. You must also write the procedure `safe?`, which determines for a set of positions, whether the queen in the kth column is `safe` with respect to the others. (Note that we need only check whether the new queen is safe – the other queens are already guaranteed safe with respect to each other.)

```scheme
(define (filter predicate sequence)
  (cond ((null? sequence) '())
        ((predicate (car sequence))
         (cons (car sequence)
               (filter predicate (cdr sequence))))
        (else (filter predicate (cdr sequence)))))

(define (enumerate-interval s e)
    (define (iter result k)
        (if (> k e)
            result
            (iter (append result (list k)) (+ k 1))))
    (iter '() s))

(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))

(define (flatmap proc seq)
  (accumulate append '() (map proc seq)))

; Version 1
; This version stores the row and col information together for each position
; Hence, the `col` parameter is never used in `safe?` procedure.

(define (adjoin-position row col rests)
    (map (lambda(x) (cons (list row col) x)) rests))

(define empty-board '(()))

(define (safe? col positions)
    (define (pos-diff x y)
        (= (abs (- (car x) (car y)))
           (abs (- (cadr x) (cadr y)))))
    (define (pos-safe? x y)
        (not (or (= (car x) (car y))
                 (pos-diff x y))))
    (if (null? positions)
        true
        (if (null? (cdar positions))
            true
            (let ((x (caar positions))
                  (y (cdar positions)))
                (accumulate (lambda (m l) (and (pos-safe? x m) l)) true y)))))

(define (queens board-size)
  (define (queen-cols k)  
    (if (= k 0)
        (list empty-board)
        (filter
         (lambda (positions) (safe? k positions))
         (flatmap
          (lambda (rest-of-queens)
            (map (lambda (new-row)
                   (adjoin-position new-row k rest-of-queens))
                 (enumerate-interval 1 board-size)))
          (queen-cols (- k 1))))))
  (queen-cols board-size))

(queens 4) ; ((((3 4) (1 3) (4 2) (2 1))) (((2 4) (4 3) (1 2) (3 1))))


; Version 2
(define empty-board '())

(define (adjoin-position row col rests)
    (cons row rests))

(define (safe? col positions)
    (define (pos-safe? row pre-row pre-col)
        (not (or (= row pre-row)
                 (= (abs (- row pre-row))
                    (abs (- col pre-col))))))
    (define (iter row pre-col pos)
        (if (null? pos)
            true
            (let ((pre-row (car pos)))
                (and (pos-safe? row pre-row pre-col)
                     (iter row (- pre-col 1) (cdr pos))))))
    (if (null? positions)
        true
        (let ((row (car positions)))
            (iter row (- col 1) (cdr positions)))))

(queens 4) ; ((3 1 4 2) (2 4 1 3))
```
