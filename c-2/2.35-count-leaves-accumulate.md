Redefine count-leaves from section 2.2.2 as an accumulation:

```scheme
(define (count-leaves t)
  (accumulate <??> <??> (map <??> <??>)))
```

**Answer**
```scheme
; Original
(define (count-leaves x)
  (cond ((null? x) 0)  
        ((not (pair? x)) 1)
        (else (+ (count-leaves (car x))
                 (count-leaves (cdr x))))))

; New version
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))

(define (map p sequence)
  (accumulate (lambda (x y) (cons (p x) y)) '() sequence))

(define (count-leaves t)
  (accumulate (lambda (x y) (+ y x)) 0 (map (lambda (x) (if (pair? x) (count-leaves x) 1)) t)))

; Verify
(count-leaves (list 1 2 (list 3 4) (list 5 (list 6 7)) 8))
```
