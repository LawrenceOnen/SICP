In case representing pairs as procedures wasn't mind-boggling enough, consider that, in a language that can manipulate procedures, we can get by without numbers (at least insofar as nonnegative integers are concerned) by implementing 0 and the operation of adding 1 as 

```scheme
(define zero (lambda (f) (lambda (x) x)))

(define (add-1 n)
  (lambda (f) (lambda (x) (f ((n f) x)))))
```

This representation is known as Church numerals, after its inventor, Alonzo Church, the logician who invented the Î» calculus.

Define `one` and `two` directly (not in terms of `zero` and `add-1`). (Hint: Use substitution to evaluate `(add-1 zero))`. Give a direct definition of the addition procedure + (not in terms of repeated application of `add-1`).

```scheme
(define one (lambda (f) (lambda (x) (f x))))

(define two (lambda (f) (lambda (x) (f (f x)))))
```

Above function can well illustrates Church numerals definition:

>The higher-order function that represents natural number n is a function that maps any function f to its n-fold composition. In simpler terms, the "value" of the numeral is equivalent to the number of times the function encapsulates its argument.

Supposed `a` and `b` are another two nonnegative integer, `(b f)` and `(a f)` is actually applying f function `b` and `a` times.  If we look into the `one` and `two` defintion we can found that `two = ((one f) (f x))`.  `(f x)` actually is `((one f) x)`.  Hence, the add operation can be composed like:  

```scheme
(define (add a b)
    (lambda (f) (lambda (x) ((a f) ((b f) x)) )))

; To verify
(define three (lambda (f) (lambda (x) (f (f (f x))))))

(define (square x) (* x x))

(define (inc x) (+ x 1))

(((add two two) square) 2) ;-> 65535

(((add two one) square) 2) ;-> 256

(((add one two) square) 2) ;-> 256

(((add zero two) square) 2) ;-> 256

(((add three two) inc) 2) ;-> 7
```
