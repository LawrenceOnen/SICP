A binary mobile consists of two branches, a left branch and a right branch. Each branch is a rod of a certain length, from which hangs either a weight or another binary mobile. We can represent a binary mobile using compound data by constructing it from two branches (for example, using list):

```scheme
(define (make-mobile left right)
  (list left right))
```

A branch is constructed from a length (which must be a number) together with a structure, which may be either a number (representing a simple weight) or another mobile:

```scheme
(define (make-branch length structure)
  (list length structure))
```

a.  Write the corresponding selectors `left-branch` and `right-branch`, which return the branches of a mobile, and `branch-length` and `branch-structure`, which return the components of a branch.

b.  Using your selectors, define a procedure `total-weight` that returns the total weight of a mobile.

c.  A mobile is said to be balanced if the torque applied by its top-left branch is equal to that applied by its top-right branch (that is, if the length of the left rod multiplied by the weight hanging from that rod is equal to the corresponding product for the right side) and if each of the submobiles hanging off its branches is balanced. Design a predicate that tests whether a binary mobile is balanced.

d.  Suppose we change the representation of mobiles so that the constructors are

```scheme
(define (make-mobile left right)
  (cons left right))
(define (make-branch length structure)
  (cons length structure))
```

How much do you need to change your programs to convert to the new representation?


**Answer**
a.

```scheme
(define (left-branch m)
    (car m))

(define (right-branch m)
    (car (cdr m)))

(define (branch-length b)
    (car b))

(define (branch-structure b)
    (car (cdr b)))
```

b.

```scheme
(define (branch-weight b total)
    (let ((s (branch-structure b)))
        (if (pair? s)
            (mobile-weight s total)
            (+ total s))))

(define (mobile-weight mb total)
    (+ (branch-weight (left-branch mb) total) (branch-weight (right-branch mb) total)))

(define (total-weight m)
    (mobile-weight m 0))

; Verify
(define sb1 (make-branch 2 3))
(define sb2 (make-branch 3 5))
(define sb3 (make-branch 4 7))
(define sm1 (make-mobile sb1 sb2))
(define sm2 (make-mobile sb2 sb3))
(define cm1 (make-mobile sb2 (make-branch 5 sm1)))
(define cm2 (make-mobile sb3 (make-branch 6 sm2)))

(total-weight sm1) ; sb1 + sb2 = 3 + 5 = 8
(total-weight sm2) ; sb2 + sb3 = 5 + 7 = 12
(total-weight cm1) ; sb2 + sm1 = 5 + 8 = 13
(total-weight cm2) ; sb3 + sm2 = 7 + 12 = 19
```

c.

```scheme
(define (mobile-balanced? m)
    (define (branch-prod b)
        (* (branch-length b) (branch-weight b 0)))
    (let* ((lb (left-branch m))
          (rb (right-branch m))
          (lbs (branch-structure lb))
          (rbs (branch-structure rb)))
        (and (= (branch-prod lb) (branch-prod rb))
             (if (pair? lbs)
                 (mobile-balanced? lbs)
                 true)
             (if (pair? rbs)
                 (mobile-balanced? rbs)
                 true))))

; Verify
; Simple balanced branch
(define sb1 (make-branch 2 3))
(define sb2 (make-branch 3 2))
(define sm1 (make-mobile sb1 sb2))

(mobile-balanced? sm1) ; #t: 2 * 3 = 3 * 2

; Simple unbalanced branch
(define sb3 (make-branch 4 7))
(define sm2 (make-mobile sb2 sb3))

(mobile-balanced? sm2) ; #f: 3 * 2 != 4 * 7

; Complex balanced branch
(define sb4 (make-branch 2 14))
(define sm3 (make-mobile sb4 sb4))
(define cm1 (make-mobile sb3 (make-branch 1 sm3)))

(mobile-balanced? cm1) ; #t: 4 * 7 = 1 * 28 and 2 * 14 = 2 * 14

; Complex branch balanced on top level but not in inner sub-mobile
(define sb5 (make-branch 4 5))
(define sb6 (make-branch 2 9))
(define sm4 (make-mobile sb5 sb6))
(define cm2 (make-mobile sb3 (make-branch 2 sm4)))

(mobile-balanced? cm2) ; #f: 4 * 7 = 2 * (5 + 9) but 2 * 9 != 4 * 5
```

d.

`right-branch` and `branch-structure` need to be revised.

```scheme
(define (right-branch m)
    (cdr m))

(define (branch-structure b)
    (cdr b))
```
